apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'signing'

group = 'de.otto'

repositories {
    mavenCentral()
}

dependencies {

    compile project(':jsonhome-core')
    compile project(':jsonhome-generator')
    compile 'org.springframework:spring-webmvc:3.1.2.RELEASE'
    compile 'cglib:cglib-nodep:2.2'
    compile 'javax.servlet:servlet-api:2.5'
    compile 'com.thoughtworks.paranamer:paranamer:2.5.1'
    testCompile 'org.testng:testng:6.3.1'
    testCompile 'org.springframework:spring-test:3.1.2.RELEASE'
}

test {
    useTestNG()
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }
            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
                name 'jsonhome-spring'
                packaging 'jar'
                description 'Spring library of the jsonhome project.'
                url 'http://github.com/otto-de/jsonhome'

                scm {
                    url 'scm:git@github.com:otto-de/jsonhome.git'
                    connection 'scm:git@github.com:otto-de/jsonhome.git'
                    developerConnection 'scm:git@github.com:otto-de/jsonhome.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'gsteinacker'
                        name 'Guido Steinacker'
                    }
                }
            }
        }
    }
}